apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.akaita.android.easylauncher'
apply plugin: "com.github.ben-manes.versions"
apply from: "${rootDir}/scripts/ChangeApkName.gradle"

android {
    compileSdkVersion(Dependencies.compileSdk)
    defaultConfig {
        applicationId "xyz.hisname.fireflyiii"
        minSdkVersion(Dependencies.minSdk)
        targetSdkVersion(Dependencies.targetSdk)
        versionCode 21
        versionName "1.2.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        def appConfigFile = file('config.properties')
        def hostName = ""
        if(appConfigFile.exists()){
            Properties appConfig = new Properties()
            appConfig.load(new FileInputStream(appConfigFile))
            if(appConfig['config.hostname'] != null){
                hostName = appConfig['config.hostname']
                manifestPlaceholders = [host: hostName.replaceAll('"', '')]
            }
        } else {
            throw new FileNotFoundException("$appConfigFile not found!")
        }
        buildConfigField("String", "HOSTNAME", hostName)
    }

    signingConfigs {
        githubRelease {
            storeFile file(System.getenv("FireflyKeystoreFile"))
            storePassword System.getenv("FireflyKeystorePassword")
            keyAlias System.getenv("FireflyKeystoreAlias")
            keyPassword System.getenv("FireflyKeystorePassword")
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            matchingFallbacks = ['release']
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"
            minifyEnabled false
            matchingFallbacks = ['debug']
        }
        // This build type is no different from `release`.
        // It's created so that it is compatible with Fdroid build system
        githubRelease{
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            matchingFallbacks = ['release']
            signingConfig signingConfigs.githubRelease
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }

    aaptOptions {
        ignoreAssetsPattern "NOTICE.txt"
    }
    configurations {all*.exclude group: 'com.android.support'}
}

afterEvaluate {
    if(build.name.contains("Github")) {
        assembleGithubRelease.dependsOn test
        assembleGithubRelease.finalizedBy(changeName)
    }
}

dependencies {

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation Dependencies.kotlinLib
    // Android support libs
    implementation Dependencies.appCompat
    implementation Dependencies.androidxFragment
    implementation Dependencies.androidxAnnotation
    implementation Dependencies.androidxRecyclerView
    implementation Dependencies.materialDesign
    implementation Dependencies.androidxCore
    implementation Dependencies.androidxConstraintLayout
    implementation Dependencies.androidxPref
    implementation Dependencies.androidxWork

    // Lifecycle component
    implementation Dependencies.lifecycleLibs
    kapt Dependencies.lifecycleCompiler

    // Room
    implementation Dependencies.roomLibs
    kapt Dependencies.roomCompiler
    // Retrofit
    implementation Dependencies.retrofitLibs
    implementation Dependencies.retrofitGson

    // Coroutine
    implementation Dependencies.coroutineCore
    implementation Dependencies.coroutineAndroid

    // Glide
    implementation Dependencies.glideLib
    kapt Dependencies.glideCompiler

    implementation Dependencies.materialDrawer
    implementation Dependencies.iconics
    implementation Dependencies.googleMaterialIcons
    implementation Dependencies.fontAwesome

    implementation Dependencies.toasty
    implementation Dependencies.threeTenBp
    implementation Dependencies.chart
    implementation Dependencies.aboutLib
    implementation Dependencies.nachos
    implementation Dependencies.acraMail
    implementation Dependencies.osmdroid
    // Project Lib
    api project(path: ":languagepack")

    testImplementation Dependencies.jUnit
    debugImplementation Dependencies.debugDb
}